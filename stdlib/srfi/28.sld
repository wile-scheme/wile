(define-library (srfi 28)
  (import (scheme base) (scheme write))
  (export format)
  (begin
    (define (format template . args)
      (let ((out (open-output-string))
            (len (string-length template)))
        (let loop ((i 0) (args args))
          (cond
            ((>= i len)
             (get-output-string out))
            ((and (char=? (string-ref template i) #\~)
                  (< (+ i 1) len))
             (let ((c (string-ref template (+ i 1))))
               (cond
                 ((char=? c #\a)
                  (if (null? args)
                      (error "format: not enough arguments")
                      (begin (display (car args) out)
                             (loop (+ i 2) (cdr args)))))
                 ((char=? c #\s)
                  (if (null? args)
                      (error "format: not enough arguments")
                      (begin (write (car args) out)
                             (loop (+ i 2) (cdr args)))))
                 ((char=? c #\%)
                  (newline out)
                  (loop (+ i 2) args))
                 ((char=? c #\~)
                  (write-char #\~ out)
                  (loop (+ i 2) args))
                 (else
                  (error "format: unknown escape" c)))))
            (else
             (write-char (string-ref template i) out)
             (loop (+ i 1) args))))))))
